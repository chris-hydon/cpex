-- The five dining philosophers for FDR

-- Bill Roscoe

-- The most standard example of them all.  We can determine how many
-- (with the conventional number being 5):

-- X = thinks.0 -> STOP
-- Y = thinks.1 -> STOP

-- assert X [] Y :[deadlock free [F]]

N = 8

PHILNAMES= {0..N-1}
FORKNAMES = {0..N-1}

channel thinks, sits, eats, getsup:PHILNAMES
channel picks, putsdown:PHILNAMES.FORKNAMES

channel done

-- A philosopher thinks, sits down, picks up two forks, eats, puts down forks
-- and gets up, in an unending cycle.	

PHIL(i) = thinks.i -> sits!i -> picks!i!i -> picks!i!((i+1)%N) ->
          eats!i -> putsdown!i!((i+1)%N) -> putsdown!i!i -> getsup!i -> done -> PHIL(i)

-- Of course the only events relevant to deadlock are the picks and putsdown
-- ones.  Try the alternative "stripped down" definition

PHILs(i) =  picks!i!i -> picks!i!((i+1)%N) ->
           putsdown!i!((i+1)%N) -> putsdown!i!i -> PHILs(i)



-- Its alphabet is

AlphaP(i) = {thinks.i, sits.i,picks.i.i,picks.i.(i+1)%N,eats.i,putsdown.i.i,
             putsdown.i.(i+1)%N,getsup.i}

-- A fork can only be picked up by one neighbour at once!

FORK(i) = picks!i!i -> putsdown!i!i -> FORK(i)
         []  picks!((i-1)%N)!i -> putsdown!((i-1)%N)!i -> FORK(i)

AlphaF(i) = {picks.i.i, picks.(i-1)%N.i, putsdown.i.i, putsdown.(i-1)%N.i}

-- We can build the system up in several ways, but certainly
-- have to use some form of parallel that allows us to
-- build a network parameterized by N.  The following uses
-- a composition of N philosopher/fork pairs, each individually
-- a parallel composition.

SYSTEM = || i:PHILNAMES@[union(AlphaP(i),AlphaF(i))] 
                       (PHIL(i)[AlphaP(i)|| AlphaF(i)] FORK(i))

-- or stripped down

SYSTEMs = || i:PHILNAMES@[union(AlphaP(i),AlphaF(i))] 
                       (PHILs(i)[AlphaP(i)|| AlphaF(i)] FORK(i))


-- As an alternative (see Section 2.3) we can create separate
-- collections of the philosophers and forks, each composed
-- using interleaving ||| since there is no communication inside
-- these groups.

PHILS = ||| i:PHILNAMES@ PHIL(i)
FORKS = ||| i:FORKNAMES@ FORK(i)

SYSTEM' = PHILS[|{|picks, putsdown|}|]FORKS

-- The potential for deadlock is illustrated by 

P = |~| x : {| picks, putsdown |} @ x -> P

assert P [T= SYSTEM' \ (diff(Events, {| done,picks, putsdown |}))
assert SYSTEM' :[deadlock free [F]]

-- or equivalently in the stripped down
-- assert SYSTEMs :[deadlock free [F]]

-- which will find the same deadlock a lot faster.

-- There are several well-known solutions to the problem.  One involves a
-- butler who must co-operate on the sitting down and getting up events,
-- and always ensures that no more than four of the five
-- philosophers are seated.

BUTLER(j) = j>0 & getsup?i -> BUTLER(j-1)
            []j<N-1 & sits?i -> BUTLER(j+1)

BSYSTEM = SYSTEM [|{|sits, getsup|}|] BUTLER(0)

-- assert BSYSTEM :[deadlock free [F]]

-- We would have to reduce the amount of stripping down for this,
-- since it makes the sits and getsup events useful...try this.

-- A second solution involves replacing one of the above right-handed (say)
-- philosophers by a left-handed one:

LPHIL(i)= thinks.i -> sits.i -> picks.i.((i+1)%N) -> picks.i.i -> 
          eats.i -> putsdown.i.((i+1)%N) -> putsdown.i.i -> getsup.i -> LPHIL(i)

ASPHILS = ||| i:PHILNAMES @ if i==0 then LPHIL(i) else PHIL(i)

ASSYSTEM = ASPHILS[|{|picks, putsdown|}|]FORKS

-- This asymmetric system is deadlock free, as can be proved using Check.  

-- assert ASSYSTEM :[deadlock free [F]]

-- If you want to run a lot of dining philosophers, the best results will
-- probably be obtained by removing the events irrelevant to ASSYSTEM
-- (leaving only picks and putsdown) in:
LPHILs(i)=  picks.i.((i+1)%N) -> picks.i.i -> 
           putsdown.i.((i+1)%N) -> putsdown.i.i -> LPHILs(i)

ASPHILSs = ||| i:PHILNAMES @ if i==0 then LPHILs(i) else PHILs(i)

ASSYSTEMs = ASPHILSs[|{|picks, putsdown|}|]FORKS

-- assert ASSYSTEMs :[deadlock free [F]]

-- Setting N=10 will show the spectacular difference in running the
-- stripped down version.  Try to undertand why there is such an
-- enormous difference.

-- Compare the stripped down versions with the idea of "Leaf Compression"
-- discussed in Chapter 8.
